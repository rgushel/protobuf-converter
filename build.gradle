buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.8.5"
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

group = 'com.github.BAData'

repositories {
    mavenCentral()
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    generatedFilesBaseDir = "$projectDir/src"
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        helixProtoConverter(MavenPublication) {
            version = "$buildVersion"

            from components.java
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            name = 'helix'
            url = "https://repo.helix.re/repository/helix/"
            credentials {
                username "$helixRepoUser"
                password "$helixRepoPassword"
            }
        }
    }
}